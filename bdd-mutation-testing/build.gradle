plugins {
	id 'java'
	id 'groovy'
	id 'org.springframework.boot' version '3.1.5'
	id 'io.spring.dependency-management' version '1.1.3'
	id 'com.diffplug.spotless' version '6.22.0'
}

group = 'pl.jcw.example.bddmutation'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '17'
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	runtimeOnly 'org.postgresql:postgresql'
	runtimeOnly 'org.testcontainers:postgresql'

	//testing dependencies
	testImplementation 'org.springframework.boot:spring-boot-testcontainers'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.testcontainers:junit-jupiter'

	//spock dependencies
	testImplementation 'com.athaydes:spock-reports:2.5.1-groovy-4.0'
	testImplementation platform('org.spockframework:spock-bom:2.3-groovy-4.0')
	testImplementation 'org.spockframework:spock-core'
	testImplementation 'org.spockframework:spock-spring'
	testImplementation 'org.testcontainers:spock'

	//lombok
	compileOnly 'org.projectlombok:lombok:1.18.30'
	annotationProcessor 'org.projectlombok:lombok:1.18.30'
	testCompileOnly 'org.projectlombok:lombok:1.18.30'
	testAnnotationProcessor 'org.projectlombok:lombok:1.18.30'
}

tasks.named('test') {
	useJUnitPlatform()
}

spotless {
	//see configuration of spotless formatter https://github.com/diffplug/spotless/tree/main/plugin-gradle
	java{
		googleJavaFormat()
		removeUnusedImports()
	}

	groovy {
		excludeJava() // excludes all Java sources within the Groovy source dirs from formatting
		greclipse() // has its own section below
	}
	groovyGradle {
		target '*.gradle' // default target of groovyGradle
		greclipse()
	}
}
